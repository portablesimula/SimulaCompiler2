
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA A/S - SIMULA TEST NO 147
%
%  PURPOSE:     Test virtual procedure by name.
%
%  METHOD:      Try some examples and see if they work as expected.
%
%  WRITTEN BY:  MYH
% ==============================================================================
external class SimulaTest;

SimulaTest(147,"--- Test virtual procedure by name") begin
   text array facit(0:33);
   integer traceCase;
   
   procedure trace(msg); text msg;
   begin
      if verbose then
      	 begin outtext(msg); outimage; end;
      if msg ne facit(traceCase) then
      begin err("Case " & edit(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;
	
	class A; virtual: procedure vP1;
			 		  procedure vP2 is procedure vP2(t); value t; text t;;
	begin
 
		procedure vP1(t); value t; text t; begin
			trace("In vP1 t=" & t);
		end;
 
		procedure vP2(t); value t; text t; begin
			trace("In vP2 t=" & t);
		end;

		begin integer kk;
			trace("Before call P1(vP1)");
	       	P1(vP1);
			trace("After call P1(vP1)");
	       	begin integer nn;
				trace("Before call P1(vP2)");
	       		P1(vP2);
				trace("After call P1(vP2)");
	       	end;
	    end;
	end;

	procedure P1(fP); name fP; procedure fP; begin
		fP(copy("AbreCaDab"));
	end;
 
	ref(A) x;
	text t1, t2;
	
	facit(0):-"At START";
	facit(1):-"Before call P1(vP1)";
	facit(2):-"In vP1 t=AbreCaDab";
	facit(3):-"After call P1(vP1)";
	facit(4):-"Before call P1(vP2)";
	facit(5):-"In vP2 t=AbreCaDab";
	facit(6):-"After call P1(vP2)";
	
	facit(7):-"Before call P1(x.vP1)";
	facit(8):-"In vP1 t=AbreCaDab";
	facit(9):-"Before call P1(x.vP2)";
	facit(10):-"In vP2 t=AbreCaDab";
	facit(11):-"At END";

	trace("At START");

    x:-new A;

	trace("Before call P1(x.vP1)");
    P1(x.vP1);

	trace("Before call P1(x.vP2)");
    P1(x.vP2);

	trace("At END");

 end;
