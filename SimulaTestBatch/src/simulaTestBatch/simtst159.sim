
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 159
%
%  PURPOSE:     Test Specified Virtual Real Functions as part of an Expression
%
%  METHOD:      Try it and see if it work as expected.
%
%  WRITTEN BY:  MYH
% ==============================================================================
external class SimulaTest;

SimulaTest(159,"--- Test Specified Virtual Real Functions as part of an Expression") begin
	text array facit(0:15);
	integer traceCase;
   
	procedure trace(msg); text msg; begin
		if verbose then
		begin outtext(msg); outimage; end;
		if msg ne facit(traceCase) then begin
			err("Case " & edit(traceCase));
			outtext("Trace: """ & msg & """"); outimage;
			outtext("Facit: """ & facit(traceCase) & """"); outimage;
		end;
		traceCase:=traceCase+1;
	end;


	ref(A) first;
	real r;

	class A;
		virtual:
			procedure virt1 is real procedure virt1;;
			procedure virt2 is real procedure virt2(p1,p2); integer p1; real p2;;
	begin
		real procedure virt1; begin virt1 := r; r := r + 1; end;
		real procedure virt2(p1,p2); integer p1; real p2; begin virt2 := r; r := r + 1; end;

		trace("At A'Statements");
	end;
	
% ========================================================
	
	facit(0) :- "At START";
	
	facit(1) :- "At A'Statements";
	facit(2) :- "r = 1.00";
	facit(3) :- "r = 1.00";
	facit(4) :- "In if not first.virt1";
	facit(5) :- "In while first.virt1";
	facit(6) :- "In while first.virt1";
	facit(7) :- "r = -30.00";
	facit(8) :- "r = -30.00";
	facit(9) :- "In if not first.virt2";
	
	facit(10) :- "At END";
	
% ========================================================

	trace("At START");
	
	first :- new A;
	r := ( - first.virt1) + r;
	trace("r = " & edfix(r,2));
	
	r := first.virt1;
	trace("r = " & edfix(r,2));

	if first.virt1 < 10 then begin
		trace("In if not first.virt1");
	end;
	
	while first.virt1 < 4 do begin
		trace("In while first.virt1");
	end;

% ========================================================

	r := ( - first.virt2(0,0)) * r;
	trace("r = " & edfix(r,2));
	
	r := first.virt2(1,2);
	trace("r = " & edfix(r,2));

	if first.virt2(3,3) < 10 then begin
		trace("In if not first.virt2");
	end;


	trace("At END");

 end;
